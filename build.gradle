plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

ext.library = [
    groovy: 'org.codehaus.groovy:groovy-all:3.0.14',
    cli: 'commons-cli:commons-cli:1.2',
    github: 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5',
    jackson_yaml: 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.6',
    jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.8.6',
    mail: 'javax.mail:mail:1.4.5',

    junit: 'junit:junit-dep:4.10',
    hamcrest: 'org.hamcrest:hamcrest-all:1.3',
    mockito: 'org.mockito:mockito-core:2.7.7',
]

def BUILD_NUMBER = project.hasProperty('BUILD_NUMBER')? "$BUILD_NUMBER" : '0'
version = "1.0.$BUILD_NUMBER"

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'

    group = 'com.jacky'
    version = "1.0.$BUILD_NUMBER"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    gradle.startParameter.continueOnFailure = true

    sourceSets.main.java.srcDirs = []
    sourceSets.test.java.srcDirs = []

    repositories {
        jcenter()
    }

    dependencies {
        implementation library.groovy
        implementation library.cli
        implementation library.github
        implementation library.jackson_databind

        testImplementation library.junit
        testImplementation library.hamcrest
    }

    test {
        defaultCharacterEncoding = "UTF-8"
        testLogging {
            showStackTraces = true
            exceptionFormat = 'full'
            events "STARTED", "PASSED", "FAILED", "SKIPPED"
        }
    }
}
